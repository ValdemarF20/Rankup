import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = 'net.valdemarf'
version = '1.0-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'Matt'
        url = "https://repo.mattstudios.me/artifactory/public/"
    }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    implementation "dev.triumphteam:triumph-gui:3.0.3"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
}

shadowJar {
    relocate("dev.triumphteam.gui", "net.valdemarf.gui")
}

processResources {
    eachFile { expand version: version }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-deprecation'
}

task buildJar(type : Copy) {
    dependsOn tasks.shadowJar

    shadowJar.archiveFileName.set('RankupShadow.jar')
    from shadowJar.archiveFile
    into project.property('server_directory')
}

configure(buildJar) {
    group = 'Rankup'
    description = 'Build and export jar'
}